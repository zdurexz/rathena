sp_cor,111,130,3	script	Elyumina1#os_merchant	10301,{
	function weapon; function armor;
	cutin "ep171_elyumina04",0;
	mes "[ Elyumina ]";
	mes "What's up? If you call a busy person, you have to atleast talk to them, no?";
	next;
	if(select("Request OS Weapons.:Request Illusion Equipments.") == 1)
		weapon();
	else
		armor();
	end;

function	armor		{
	mes "[ Elyumina ]";
	mes "For Illusion Equipments, I will need 50 <ITEM>[Cor Cores]<INFO>25723</INFO></ITEM>.";
	mes "Let's see... this should be good, which one do you want?";
	next;
	.@s = select("Illusion Armor:Illusion Engine Wing:Illusion Leg:Illusion Booster:Illusion Battle Chip") - 1;
	explode(.@T$,.armor$[.@s],",");
	cutin "ep171_elyumina04",0;
	mes "[ Elyumina ]";
	for(.@i = 0; .@i < 2; .@i++){
		.@item_id[.@i] = atoi(.@T$[.@i]);
		.@item_name$[.@i] = getitemname(.@item_id[.@i]);
		.@menu$ +=  .@item_name$[.@i] + ":";
		mes "<ITEM>["+.@item_name$[.@i]+"]<INFO>"+.@item_id[.@i]+"</INFO></ITEM>";
	}
	mes "Which will it be?";
	next;
	.@s = select(.@menu$) - 1;
	mes "[ Elyumina ]";
	mes "Yeah, what you want is a " + .@item_name$[.@s] + ", right? I will need 50 Cor Cores.";
	next;
	if(select("Exchange:Do not exchange") == 2){
		cutin "ep171_elyumina03",0;
		mes "[ Elyumina ]";
		mes "Are you kidding me? I've been very careful!";
		close3;
	}
	if(countitem(25723) < 50){
		cutin "ep171_elyumina03",0;
		mes "[ Elyumina ]";
		mes "Are you kidding me? You don't have enough Cor Core pieces!";
		close3;
	}
	delitem 25723,50;
	mes "[ Elyumina ]";
	mes "Here, " + .@item_name$[.@s] + "! Take it, don't forget to upgrade it with modules because it will be the best armor you've ever had.";
	getitem .@item_id[.@s],1;
	close3;
}

function	weapon	{
	cutin "ep171_elyumina04",0;
	mes "[ Elyumina ]";	
	mes "OS weapons? I need 1 <ITEM>[Broken Weapon]<INFO>25668</INFO></ITEM> and 50 <ITEM>[Unknown Parts]<INFO>25669</INFO></ITEM>.";
	mes "This is easy for me, I'll fix any of these unfinished weapons.";
	next;
	if(select("Get OS Weapon.:Check all weapon list.") == 2){
		cutin "ep171_elyumina03",0;
		mes "[ Elyumina ]";
		mes "Tsk, even if you pick everything, you can only have one in there, at random!";
		next;
		mes "[ Elyumina ]";
		for(.@i = 0; .@i < getarraysize(.weapon); .@i++){
			.@item_name$ = getitemname(.weapon[.@i]);
			mes "<ITEM>["+.@item_name$+"]<INFO>"+.weapon[.@i]+"</INFO></ITEM>";
		}
		close3;
	} else {
		if(countitem(25668) < 1 || countitem(25669) < 50){
			cutin "ep171_elyumina03",0;
			mes "[ Elyumina ]";
			mes "I told you I need 1 Broken Weapon and 50 Unknown Parts. Open your ears and listen!";
			close3;
		}
		delitem 25668,1;
		delitem 25669,50;
		.@weapon = .weapon[rand(getarraysize(.weapon))];
		cutin "ep171_elyumina04",0;
		mes "[ Elyumina ]";	
		mes "Now, take it. If you need anything else, come back with more broken weapons and unknown parts.";
		getitem .@weapon,1;
		close3;	
	}
}

OnInit:
	setarray .armor$,
	"15376,15377",
	"20933,20934",
	"22196,22197",
	"32207,32208",
	"32209,32210";	
	//setarray .weapon,1862,13493,16088,16089,18178,18179,18180,21047,26151,26164,28038,28136,28253,28755,28269,32019;
	setarray .weapon,1862,13493,16088,16089,18178,18179,18180,21047,26151,26164,28038,28136,28253,28629,28755,32019;
	end;	
}