//===== rAthena Script =======================================
//= Banquet Quests
//===== Description: =========================================
//= [Walkthrough Conversion]
//--- NPCs quests for Episode 16.1
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================
// Exchanges and enchants npc

moc_para01,178,32,4	script	Hogar#option1	456,{
	disable_items;
	if ((MaxWeight - Weight) < 1000) {
		mes "ดูเหมือนว่าจะดำเนินการต่อไปได้ยากเนื่องจากเจ้าแบกของมาหนักเกินไป. ลองลดน้ำหนักลงแล้วมาใหม่.";
		close;
	}
	if (checkweight(501,1) == 0) {
		mes "มีจำนวนของอยู่ในตัวมากเกินไป. ลองลดของลงแล้วมาใหม่.";
		close;
	}
	
	mes "[Hogar]";
	mes "เจ้ามาที่นี่เพื่อค้าขายกับข้าใช่ไหม ? บอกสิ่งที่เจ้าต้องการตอนนี้.";
	next;
	.@s = select( "บอกข้าเกี่ยวกับรายละเอียด.", "ออฟชุด", "ลบออฟชุด" );
	switch(.@s) {
	case 1:
		mes "[Hogar]";
		mes "ตั้งใจฟังให้ดี. ข้าจะไม่พูดซ้ำสอง.";
		next;
		mes "[Hogar]";
		mes "ข้าสามารถออฟ Agenda Robe หรือ Consultation Robe ได้โดยต่อหนึ่งช่องออฟใช้ 10 Schwartzvald Honor Marks. ข้าออฟได้ 2 ช่อง. และข้าใช้ 5 Schwartzvald Honor Marks สำหรับลบออฟ. เจ้าฟังทันไหม ?";
		next;
		mes "[Hogar]";
		mes "ข้าไม่ลบออฟให้ทุกครั้ง. ข้าจะทำก็ต่อเมื่อออฟของไปแล้ว หรือจนกว่าเจ้าไม่สามารถออฟไปได้มากกว่านี้. และ... จากนั้น...";
		next;
		mes "[Hogar]";
		mes "ข้าหวังว่าจะเป็นการค้าขายที่ดี, คู่หู.";
		close;
	case 2:
		mes "[Hogar]";
		mes "เจ้าต้องมี 10 Schwartzvald Honor Marks สำหรับ Agenda Robe หรือ Consultation Robe ต่อหนึ่งช่องออฟ. เจ้าต้องการออฟเลยไหม ?";
		break;
	case 3:
		mes "[Hogar]";
		mes "เจ้าต้องมี 5 Schwartzvald Honor Marks ในการลบออฟ Consultation Robe หรือ Agenda Robe. ของสวมใส่จะไม่หายไป. การ์ดและเลเวลตีบวกจะยังคงเดิม.";
		break;
	}
	next;
	if (select( "หลบออกมา.", "ทำมันเลย." ) == 1) {
		mes "[Hogar]";
		mes "ข้ายุ่งมากดังนั้นอย่ามาพูดกับข้าโดยไม่มีธุระ.";
		close;
	}
	switch(.@s) {
	case 2:
		.@item_id = getequipid(EQI_ARMOR);
		.@refine = getequiprefinerycnt(EQI_ARMOR);
		.@card[0] = getequipcardid(EQI_ARMOR,0);
		.@card[1] = getequipcardid(EQI_ARMOR,1);
		.@card[2] = getequipcardid(EQI_ARMOR,2);
		.@card[3] = getequipcardid(EQI_ARMOR,3);

		if (.@item_id == -1) {
			mes "[Hogar]";
			mes "เจ้าถอดมันออก.";
			close;
		}
		if (.@item_id != 15164 && .@item_id != 15163) {
			mes "[Hogar]";
			mes "หยุดเล่นตลกได้แล้ว. ถ้าเจ้าอยากจะออฟของ, ให้สวมใส่มันมาด้วย.";
			close;
		}
		if (.@card[2] > 0 && .@card[3] > 0) {
			mes "[Hogar]";
			mes "ไม่สามารถออฟไปได้มากกว่านี้. เจ้าควรไปจัดการกับออฟก่อน ?";
			close;
		}
		if (countitem(25155) < 10) {
			mes "[Hogar]";
			mes "เฮ้, คู่หู. เจ้าไม่สามารถค้าขายกับข้าได้ถ้าเจ้าไม่มี Schwartzvald Honor Marks.";
			close;
		}

		if (.@item_id == 15164) {// Consultative Robe
			if (.@card[3] == 0) {
				setarray .@enhance[0],
					25,   4994,	// Rune of Strength Lv 1
					105,  4997,	// Rune of Agility Lv 1
					110, 29009,	// Rune of Vitality Lv 1
					170, 29000,	// Rune of Intellect Lv 1
					230, 29003,	// Rune of Dexterity Lv 1
					255, 29006,	// Rune of Luck Lv 1
					265,  4995,	// Rune of Strength Lv 2
					315,  4998,	// Rune of Agility Lv 2
					317, 29010,	// Rune of Vitality Lv 2
					352, 29001,	// Rune of Intellect Lv 2
					387, 29004,	// Rune of Dexterity Lv 2
					407, 29007,	// Rune of Luck Lv 2
					412,  4996,	// Rune of Strength Lv 3
					432,  4999,	// Rune of Agility Lv 3
					452, 29002,	// Rune of Intellect Lv 3
					467, 29005,	// Rune of Dexterity Lv 3
					477, 29008;	// Rune of Luck Lv 3
				.@card_index = 3;
			}
			else {
				setarray .@enhance[0],
					60,   4994,	// Rune of Strength Lv 1
					80,  29000,	// Rune of Intellect Lv 1
					160, 29009,	// Rune of Vitality Lv 1
					180, 29003,	// Rune of Dexterity Lv 1
					220, 29006,	// Rune of Luck Lv 1
					270,  4995,	// Rune of Strength Lv 2
					275,  4998,	// Rune of Agility Lv 2
					325, 29010,	// Rune of Vitality Lv 2
					350, 29001,	// Rune of Intellect Lv 2
					365, 29004,	// Rune of Dexterity Lv 2
					405, 29007,	// Rune of Luck Lv 2
					420,  4996,	// Rune of Strength Lv 3
					425,  4999,	// Rune of Agility Lv 3
					435, 29002,	// Rune of Intellect Lv 3
					455, 29011,	// Rune of Vitality Lv 3
					465, 29005,	// Rune of Dexterity Lv 3
					480, 29008;	// Rune of Luck Lv 3
				.@card_index = 2;
			}
		}
		else {// Agenda Robe
			if (.@card[3] == 0) {
				setarray .@enhance[0],
					50,   4994,	// Rune of Strength Lv 1
					100,  4997,	// Rune of Agility Lv 1
					150, 29009,	// Rune of Vitality Lv 1
					190, 29000,	// Rune of Intellect Lv 1
					230, 29003,	// Rune of Dexterity Lv 1
					270, 29006,	// Rune of Luck Lv 1
					305,  4995,	// Rune of Strength Lv 2
					340,  4998,	// Rune of Agility Lv 2
					390, 29010,	// Rune of Vitality Lv 2
					425, 29001,	// Rune of Intellect Lv 2
					460, 29004,	// Rune of Dexterity Lv 2
					495, 29007,	// Rune of Luck Lv 2
					505,  4996,	// Rune of Strength Lv 3
					520,  4999,	// Rune of Agility Lv 3
					530, 29011,	// Rune of Vitality Lv 3
					545, 29002,	// Rune of Intellect Lv 3
					555, 29005,	// Rune of Dexterity Lv 3
					565, 29008;	// Rune of Luck Lv 3
				.@card_index = 3;
			}
			else {
				setarray .@enhance[0],
					70,   4994,	// Rune of Strength Lv 1
					72,   4997,	// Rune of Agility Lv 1
					147, 29009,	// Rune of Vitality Lv 1
					177, 29000,	// Rune of Intellect Lv 1
					197, 29003,	// Rune of Dexterity Lv 1
					247, 29006,	// Rune of Luck Lv 1
					297,  4995,	// Rune of Strength Lv 2
					307,  4998,	// Rune of Agility Lv 2
					382, 29010,	// Rune of Vitality Lv 2
					412, 29001,	// Rune of Intellect Lv 2
					437, 29004,	// Rune of Dexterity Lv 2
					487, 29007,	// Rune of Luck Lv 2
					507,  4996,	// Rune of Strength Lv 3
					510,  4999,	// Rune of Agility Lv 3
					540, 29011,	// Rune of Vitality Lv 3
					555, 29002,	// Rune of Intellect Lv 3
					563, 29005,	// Rune of Dexterity Lv 3
					573, 29008;	// Rune of Luck Lv 3
				.@card_index = 2;
			}
		}
		.@size = getarraysize(.@enhance);
		.@r = rand( 1,.@enhance[.@size-2] );
		for ( .@i = 0; .@i < .@size && .@enhance[.@i] < .@r; .@i += 2 )
			continue;
		.@card[ .@card_index ] = .@enhance[.@i+1];
		delitem 25155,10;
		delequip EQI_ARMOR;
		getitem2 .@item_id,1,1,.@refine,0,.@card[0],.@card[1],.@card[2],.@card[3];
		break;
	case 3:
		.@item_id = getequipid(EQI_ARMOR);
		.@refine = getequiprefinerycnt(EQI_ARMOR);
		.@card[0] = getequipcardid(EQI_ARMOR,0);
		.@card[2] = getequipcardid(EQI_ARMOR,2);
		.@card[3] = getequipcardid(EQI_ARMOR,3);
		if (.@item_id == -1) {
			mes "[Hogar]";
			mes "เจ้าถอดมันออก.";
			close;
		}
		if (.@item_id != 15163 && .@item_id != 15164) {
			mes "[Hogar]";
			mes "หยุดเล่นตลกได้แล้ว. ถ้าเจ้าอยากจะลบออฟของ, ให้สวมใส่มันมาด้วย.";
			close;
		}
		if (.@card[2] == 0 || .@card[3] == 0) {
			mes "[Hogar]";
			mes "โอ้, ไอเท็้มไม่เคยออฟมาก่อน. ข้าลบออฟให้ไม่ได้.";
			close;
		}
		if (countitem(25155) < 5) {
			mes "[Hogar]";
			mes "เฮ้, คู่หู. เจ้าไม่สามารถค้าขายกับข้าได้ถ้าเจ้าไม่มี Schwartzvald Honor Marks.";
			close;
		}
		delitem 25155,5;
		delequip EQI_ARMOR;
		getitem2 .@item_id,1,1,.@refine,0,.@card[0],0,0,0;
		break;
	}
	mes "[Hogar]";
	mes "หืม, สำเร็จไปได้ด้วยดี. ตรวจดูของด้วย.";
	specialeffect2 EF_REPAIRWEAPON;
	close;
}
