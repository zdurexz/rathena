prontera,184,210,4	script	Auction Manager	790,{
	doevent "auction_bid_main::OnTalk";
	end;
	
OnInit:
waitingroom "กิจกรรมประมูลของ",0;
end;	
}


-	script	auction_bid_main	FAKE_NPC,{
	function func_getitemname;
	
	function	func_getitemname	{
		.@refine = getarg( 0,0 );
		.@itemid = getarg( 1,0 );
		.@amount = getarg( 2,0 );
		
		.@name$ = getitemname( .@itemid );
		
		if ( .@name$ != "null" && .@amount > 0 ) {
			.@slot = getitemslots( .@itemid );
			.@itemtype = getiteminfo( .@itemid,2 );
			
			if ( .@itemtype == IT_ARMOR || .@itemtype == IT_WEAPON ) {
				.@name$ = .@name$ + " ["+.@slot+"]";
				if ( .@refine ) 
				.@name$ = "+" + .@refine +" "+.@name$;
			}
			
			if ( .@amount > 1 ) {
				.@name$ = .@amount + "x " + .@name$;
			}
		}
		else {
			.@name$ = "ID#"+.@itemid;
		}
		
		return .@name$;
	}
	
	OnInit:
		.gm_level = 99;
		.npc_name$ = strnpcinfo(3);
		bindatcmd "auction",.npc_name$+"::OnAtcommand",.gm_level,.gm_level;
		end;
		
	OnTalk:
		.@aid = getcharid(3);
		.@cid = getcharid(0);
		
		mes "กิจกรรมประมูล";
		switch ( .status ) {
			default:
				.@is_gm = ( getgmlevel() >= .gm_level && .gm_level > 0 );
				switch ( select( 
					"รับสินค้าที่ประมูล",
					( .@is_gm ) ? "^FF0000[GM]^000000 Manage Auction Item(s)" : "",
					( .@is_gm && !.status ) ? "^FF0000[GM]^000000 Start Auction" : "",
					( .@is_gm && .status ) ? "^FF0000[GM]^000000 End Auction" : "",
					"ดูรายละเอียด"
				) ) {
					case 1:
						.@sql$ = "SELECT `id`,`nameid`,`refine`,`amount`,`bid`,`date` FROM `e_auction_list_log` WHERE `aid` = "+.@aid+" LIMIT 50";
						query_sql( .@sql$, .@id, .@nameid, .@refine, .@amount, .@bid, .@date$ );
						.@size = getarraysize( .@id );
						if ( !.@size ) {
							mes "ไม่มี Item ที่ประมูลได้.";
						}
						else {
							for ( .@i = 0; .@i < .@size; .@i++ )
								.@menu$ = .@menu$ + replacestr( func_getitemname( .@refine[.@i], .@nameid[.@i], .@amount[.@i] ), ":", " " ) + ":";
							next;
							.@i = select( .@menu$ ) - 1;
							mes "คุณได้ประมูล "+func_getitemname( .@refine[.@i], .@nameid[.@i], .@amount[.@i] )+" ราคา "+F_InsertComma( .@bid[.@i] )+" Zeny. ต้องการรับของตอนนี้เลยมั้ยล่ะ.";
							if ( Zeny >= .@bid[.@i] )
								if ( select( "ชำระเงิน","Cancel" ) == 1 ) {
									if ( !checkweight( .@nameid[.@i], .@amount[.@i] ) ) {
										mes "สินค้ามีน้ำหนักมากเกินไป โปรดตรวจสอบ.";
									}
									else {
										Zeny -= .@bid[.@i];
										.@sql$ = "delete from `e_auction_list_log` WHERE `id` = "+.@id[.@i]+" LIMIT 1";
										query_sql( .@sql$ );
										getitem2 .@nameid[.@i],.@amount[.@i],1,.@refine[.@i],0,0,0,0,0;
										mes "ได้รับ "+func_getitemname( .@refine[.@i], .@nameid[.@i], .@amount[.@i] );
									}
								}
						}
						break;
					case 2:
						mes "รายการ Item ประมูล :";
						.@sql$ = "SELECT `id`,`nameid`,`refine`,`amount`,`bid_min`,`bid_max`,`minute`,`date` FROM `e_auction_bid_list` WHERE `status` = 1 LIMIT 50";
						query_sql( .@sql$, .@id, .@nameid, .@refine, .@amount, .@bid_min, .@bid_max, .@minute, .@date$ );
						.@size = getarraysize( .@id );
						
						if ( !.@size ) {
							mes " ~ none";
						}
						else {
							for ( .@i = 0; .@i < .@size; .@i++ ) {
								.@name$ = func_getitemname( .@refine[.@i], .@nameid[.@i], .@amount[.@i] );
								.@menu$ = .@menu$ + replacestr( .@name$, ":", " " ) + ":";
								mes " ~ "+.@name$;
							}
						}
						next;
						
						switch( select( 
							"Add Auction Item",
							( .@size ) ? "Remove Auction Item": "",
							"Cancel"
						) ) {
							case 1:
								do {

									if ( .@new_nameid ) {
										.@type = getiteminfo( .@new_nameid,2 );
										.@is_equipment = ( .@type != IT_ARMOR && .@type != IT_WEAPON );
										if ( .@is_equipment )
											.@new_refine = 0;
										
										if ( .@new_amount <= 0 )
											.@new_amount = 1;
									}
									
									.@new_incomplete = ( 
										getitemname( .@new_nameid ) == "null"
										|| .@new_amount <= 0
										|| .@new_bid_min <= 0
										|| .@new_bid_max <= .@new_bid_min
										|| .@new_minute <= 0
									);
									
									mes "[Setup - Auction Item]";
									mes "Item : "+func_getitemname( .@new_refine, .@new_nameid, .@new_amount );
									mes "Bid: "+F_InsertComma( .@new_bid_min )+" ~ "+F_InsertComma( .@new_bid_max )+" Zeny";
									mes "Bid Duration: "+.@new_minute+" minute(s)";
									
									switch ( select( 
										"Edit ItemID",
										( .@is_equipment ) ? "" : "Edit Refine",
										"Edit Amount",
										"Edit Minimum Bid",
										"Edit Maximum Bid",
										"Edit Bid Duration",
										( .@new_incomplete ) ? "" : "Complete",
										"Cancel"
									) ) {
										case 1:
											input .@new_nameid,0,132767;
											break;
										case 2:
											input .@new_refine,0,MAX_REFINE;
											break;
										case 3:
											input .@new_amount,1,30000;
											break;
										case 4:
											input .@new_bid_min,0,MAX_ZENY;
										case 5:
											input .@new_bid_max,.@new_bid_min,MAX_ZENY;
											break;
										case 6:
											input .@new_minute,0,60;
											break;
										case 7:
											.@sql$ = "INSERT INTO `e_auction_bid_list` ( `nameid`,`refine`,`amount`,`bid_min`,`bid_max`,`minute`,`date` ) VALUES ( "+.@new_nameid+","+.@new_refine+","+.@new_amount+","+.@new_bid_min+","+.@new_bid_max+","+.@new_minute+", NOW() ) ";
											query_sql( .@sql$ );
											mes "Item Added";
										default:
										 close;
									}
									next;
								} while ( 1 );
								break;
							case 2:
								.@i = select( .@menu$ ) - 1;
								.@sql$ = "UPDATE `e_auction_bid_list` SET `status` = 2 WHERE `id` = "+.@id[.@i]+" LIMIT 1";
								query_sql( .@sql$ );
								mes func_getitemname( .@refine[.@i], .@nameid[.@i], .@amount[.@i] ) + " removed from auction.";
								break;
							default:
								break;
						}
						break;
					case 3:
						mes "กิจกรรมประมูล starting...";
						donpcevent .npc_name$+"::OnStart";
						break;
					case 4:
						mes "กิจกรรมประมูล terminating...";
						donpcevent .npc_name$+"::OnTerminate";
						break;
					default: 
						mes "กิจกรรมประมูล จะถูกจัดขึ้นโดย GM ใช้ Zeny ในการประมูล. ";
						mes "ผู้เล่นสามารถจ่ายเงินเพื่อทำการประมูลของได้ ในช่วงเวลากิจกรรม.";
						break;
				
				}
				break;
			case 1:
				mes "กำลังดำเนินการ  โปรดตรวจสอบ.";
				break;
			case 2:
				set .bid_min2, .bid_min/90;
				set .bid_min3, .bid_min+.bid_min2;
				mes "Item ที่กำลังประมูล :";
				mes "^0055FF"+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+"^000000";
				mes "ราคาประมูล "+F_InsertComma( .bid_min )+" - "+F_InsertComma( .bid_max )+" Zeny";
				mes "ขั้นต่ำที่ต้องจ่าย "+F_InsertComma( .bid_min3 )+"";
				mes " ";
				next;
				input .@zeny,0,Zeny;
				if ( .@zeny ) {
					if ( Zeny < .@zeny || .@zeny < .bid_min3 || .@zeny > .bid_max ) {
						mes "ยอดเงิน ไม่ถูกต้อง.";
					}
					else {
						mes "จ่ายเงิน "+F_InsertComma( .@zeny )+" Zeny เพื่อประมูล?";
						if ( select( "Yes","Cancel" ) == 1 ) {
							if (.bid_minute < 2) { set .bid_minute , .bid_minute+1; set .bid_addtime,1; }
							.@sql$ = "INSERT INTO `e_auction_bid_log` ( `aid`,`cid`,`bid`,`time` ) VALUES ( "+.@aid+","+.@cid+","+.@zeny+",NOW() ) ON DUPLICATE KEY UPDATE `bid` = "+.@zeny+",`time` = NOW() ";
							query_sql( .@sql$ );
							set .bid_min , .@zeny;
							if (.@zeny == .bid_max ) { donpcevent .npc_name$+"::OnEnd"; }
							if (.bid_addtime != 0) { announce "<กิจกรรมประมูล> '"+strcharinfo(0)+"' จ่ายเงิน "+F_InsertComma( .@zeny )+" Zeny เพื่อประมูล "+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+" เพิ่มเวลาประมูล 1 นาที .",bc_all; }
							else { announce "<กิจกรรมประมูล> '"+strcharinfo(0)+"' จ่ายเงิน "+F_InsertComma( .@zeny )+" Zeny เพื่อประมูล "+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+".",bc_all; }
							mes "คุณได้ประมูลของแล้ว";
						}
					}
				}
				break;
		}
		close;
		
	OnAtcommand:
		if ( compare( .@atcmd_parameters$[0], "start" ) ) {
			if ( .status ) {
				dispbottom "Event already running...";
			}
			else {
				dispbottom "Event starting...";
				donpcevent .npc_name$+"::OnStart";
			}
		}
		else if ( compare( .@atcmd_parameters$[0], "stop" ) ) {
			if ( !.status ) {
				dispbottom "Event isn't running...";
			}
			else {
				dispbottom "Event terminating...";
				donpcevent .npc_name$+"::OnEnd";
			}
		}
		else {
			dispbottom "Usage: "+.@atcmd_command$+" <start/stop>";
		}
		end;
		
	OnStart:
		if ( !.status ) {
			.@sql$ = "SELECT `id`,`nameid`,`refine`,`amount`,`bid_min`,`bid_max`,`minute` "
					+ "FROM `e_auction_bid_list` "
					+ "WHERE `status` = 1 "
					+ "AND `bid_min` <= `bid_max` "
					+ "AND `bid_min` > 0 "
					+ "AND `minute` > 0 "
					+ "AND `status` = 1 "
					+ "ORDER BY RAND() LIMIT 1";
			query_sql( .@sql$ , .@bid_id, .bid_nameid, .bid_refine, .bid_amount, .bid_min, .bid_max, .bid_minute );
			
			if ( .@bid_id <= 0 || .bid_nameid <= 0 || .bid_amount <= 0 || .bid_min > .bid_max || .bid_minute <= 0 ) {
				announce "<กิจกรรมประมูล> Cancelled due to invalid settings for 'Item-"+.@bid_id+"' detected.",bc_all;
			}
			else {
				.status = 1;
				for ( .@i = 3; .@i > 0 && .status; .@i-- ) {
					announce "<กิจกรรมประมูล> การประมูล "+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+" จะเริ่มในอีก "+.@i+" นาที",bc_all;
					sleep 60000;
				}
				if ( .status ) {
					query_sql( "TRUNCATE `e_auction_bid_log`" );
					
					.bid_timestamp = gettimetick(2);
					.@sql$ = "INSERT INTO `e_auction_list_log` ( `id`,`nameid`,`refine`,`amount`,`date` ) VALUES ( "+.bid_timestamp+", "+.bid_nameid+", "+.bid_refine+", "+.bid_amount+", NOW() )";
					query_sql( .@sql$ );
					.status = 2;
					announce "<กิจกรรมประมูล> เริ่มการประมูล "+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+" ราคา "+F_InsertComma( .bid_min )+" ~ "+F_InsertComma( .bid_max )+" Zeny ระยะเวลา "+.bid_minute+" นาที",bc_all;
					
					initnpctimer;
				}
				else donpcevent .npc_name$+"::OnEnd";
			}
		}
		end;
		
	OnEnd:
		if ( .status ) {
			if ( .status >= 2 ) {
				.@sql$ = "SELECT `aid`,`cid`,`bid`,`time` FROM `e_auction_bid_log` ORDER BY `bid` DESC LIMIT 1";
				query_sql( .@sql$, .@aid, .@cid, .@bid, .@time$ );
				sleep 1000;
				.@sql$ = "UPDATE `e_auction_list_log` SET `aid` = "+.@aid+", `cid` = "+.@cid+", `bid` = "+.@bid+" WHERE `id` = "+.bid_timestamp+" LIMIT 1";
				query_sql( .@sql$ );
				.@sql$ = "SELECT `name` FROM `char` WHERE `char_id` = "+.@cid+" LIMIT 1";
				query_sql( .@sql$,.@name$ );
				announce "<กิจกรรมประมูล> "+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+" ถูกประมูลโดย "+.@name$+" ในราคา "+F_InsertComma( .@bid )+" Zeny.",bc_all;
				.@sql$ = "UPDATE `e_auction_bid_list` SET `status` = 2 WHERE `id` = "+.bid_nameid+" ";
				query_sql( .@sql$ );
			}
			else {
				announce "<กิจกรรมประมูล> Terminated",bc_all;
			}
			.bid_nameid = 0;
			.bid_refine = 0;
			.bid_amount = 0;
			.bid_min = 0;
			.bid_max = 0;
			.bid_minute = 0;
			.bid_timestamp = 0;
			.status = 0;
		}
		end;
		
	OnTerminate:
		if ( !.status ) end;
		if ( .status == 1 )
			awake .npc_name$;
		.bid_minute = 0;
	
	OnTimer60000:
		if ( .status ) {
			.bid_minute--;
			
			if ( .bid_minute > 0 ) {
				if ( .bid_minute > 0 && ( .bid_minute <= 3 || .bid_minute % 5 == 0 )) {
					announce "<กิจกรรมประมูล> "+func_getitemname( .bid_refine, .bid_nameid, .bid_amount )+" | ราคาประมูล - "+F_InsertComma( .bid_min )+" ~ "+F_InsertComma( .bid_max )+" Zeny | เหลือเวลา - "+.bid_minute+" นาที",bc_all;
				}
				setnpctimer 0;
			}
			else {
				stopnpctimer;
				donpcevent .npc_name$+"::OnEnd";
			}
		}
		end;
}


